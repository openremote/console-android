// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext.kotlin_version = "1.8.0"
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.5.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.google.gms:google-services:4.4.2'
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

plugins {
    id 'io.github.gradle-nexus.publish-plugin' version '2.0.0'
    id 'pl.allegro.tech.build.axion-release' version '1.18.16'
}

scmVersion {
    releaseOnlyOnReleaseBranches = true
    releaseBranchNames = ['main']
    unshallowRepoOnCI.set(true)
    versionCreator('simple')
    versionIncrementer('incrementMinor')
    repository {
        remote.set("origin")
    }
    tag {
        prefix.set("")
        initialVersion({config, position -> '1.2.0'})
    }
}

group = 'io.openremote'
version = scmVersion.version

println "Build version: ${version}"

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl = uri(findProperty("releasesRepoUrl"))
            snapshotRepositoryUrl = uri(findProperty("snapshotsRepoUrl"))
            username = findProperty("publishUsername")
            password = findProperty("publishPassword")
        }
    }
}

allprojects {
    project.version = scmVersion.version
    repositories {
        google()
        mavenCentral()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
